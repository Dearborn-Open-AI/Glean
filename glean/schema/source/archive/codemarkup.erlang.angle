# Copyright (c) Facebook, Inc. and its affiliates.

schema codemarkup.erlang.1 {
import src.1
import code.20
import codemarkup.types.1
import erlang.1

#
# Resolving locations to entities
#

predicate ErlangResolveLocation:
  {
    location: codemarkup.types.Location,
    entity: code.Entity,
  }
  { { Name, File, { span = Span }, nothing }, Entity } where
    # Location.name is weird but we aren't using it for anything yet.
    # Weirdness is that distinct fucntions foo/1 and foo/2 have same Location.name ("foo").
    # Use `Name = Fqn.name + str(Fqn.arity)` when primops available so names are "foo/1" and "foo/2"
    Declaration = erlang.Declaration { func = { fqn = {name = Name}, file = File, span = Span } };
    Entity = code.Entity { erlang = { decl = Declaration } };

#
# Finding entities' locations
#

predicate ErlangEntityLocation:
  {
    entity: code.Entity,
    location: codemarkup.types.Location,
  }
  { { erlang = { decl = D } }, { Name, File, { span = Span }, nothing } } where
    erlang.DeclarationLocation { declaration = D, file = File, span = Span };
    # Location.name here is weird, see ErlangResolveLocation comment
    { func = erlang.FunctionDeclaration { fqn = {name = Name } , file = _, span = _} } = D;

#
# Finding references in a file
#

predicate ErlangFileEntityXRefLocations:
  {
    file: src.File,
    xref: codemarkup.types.XRefLocation,
    entity: code.Entity,
  }
  {File, { target = TargetLocation, source = { span = Span } }, Entity} where
  erlang.XRefsViaFqnByFile { file = File, xrefs = XRefs };
  { target = Fqn, source = Span } = XRefs[..];
  erlang.DeclarationWithFqn { fqn = Fqn, declaration = Decl };
  Entity = code.Entity { erlang = { decl = Decl } };
  # Location.name here is weird, see ErlangResolveLocation comment
  { func = { fqn = { name = Name }, file = TargetFile, span = TargetSpan } } = Decl;
  { Name, TargetFile, { span = TargetSpan }, nothing } = TargetLocation;

#
# Language entity uses
#

predicate ErlangEntityUses:
  {
    target: code.Entity,
    file: src.File,
    span: src.ByteSpan,
  }
  { { erlang = { decl = D } }, File, Span } where
  erlang.DeclarationUses { declaration = D, file = File, span = Span}

#
# Language-specific symbol info
#

predicate ErlangEntityKind:
  {
    entity: code.Entity,
    kind: codemarkup.types.SymbolKind,
  }
  { { erlang = { decl = { func = D } } }, Function } where
  erlang.FunctionDeclaration {} = D

# For backward compatibility:
predicate ErlangEntityInfo:
  {
    entity: code.Entity,
    info: codemarkup.types.SymbolInfo,
  }
  { E, { kind = K, isAbstract = false } } where
  ErlangEntityKind { E, K }

}

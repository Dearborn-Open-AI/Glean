# Copyright (c) Facebook, Inc. and its affiliates.

schema codemarkup.cxx.4 {
import src.1
import code.cxx.4
import codemarkup.types.1
import cxx1.5

# Resolving locations to entities

# Resolve locations for all traces in a Cxx file
predicate CxxResolveLocation:
  {
    location: codemarkup.types.Location,
    entity: code.cxx.Entity,
  }
  { { Str, File, Range, Span }, Entity } where
    Trace = cxx1.Trace { file = File };
    CxxResolveTraceLocation { Trace, { Str, File, Range, Span }, Entity };

# Like FileEntityLocations but indexed by a specific Trace (for Cxx)
# (deprecated, use CxxResolveTraceLocation)
predicate CxxFileEntityTraceLocations:
  {
    file: src.File,
    trace: cxx1.Trace,
    location: codemarkup.types.Location,
    entity: code.cxx.Entity,
  }
  { File, Trace, Location, Entity } where
    CxxResolveTraceLocation { Trace, Location, Entity };
    codemarkup.types.Location { file = File } = Location;

# Resolve locations for Cxx, but for only a specific trace
# We'll need to pick the trace on the client
predicate CxxResolveTraceLocation:
  {
    trace : cxx1.Trace,
    location: codemarkup.types.Location,
    entity: code.cxx.Entity,
  }
  { Trace, Location, Entity } where
    Trace = cxx1.Trace { file = File, declarations = cxx1.Declarations Decls };
    # regular decls
    (
      Decl = Decls[..];
      cxx1.DeclarationLocationNameSpan { Decl, Range, Str, _, Span };
      Location = codemarkup.types.Location { Str, File, { range = Range }, { just = Span } };
      CxxResolveDeclarationToEntity { Decl, Entity };
    )

# For a given declaration, match to proper code.cxx.Entity. It will be
# a definition entity if the given declaration points to a definition.
predicate CxxResolveDeclarationToEntity:
  {
    decl: cxx1.Declaration,
    entity: code.cxx.Entity,
  }
  { Decl, Entity } where
    if (
      { function_ = FunctionDecl } = Decl;
      FunctionDefn = cxx1.FunctionDefinition { declaration = FunctionDecl }
    ) then (
      { defn = { function_ = FunctionDefn } } = Entity
    ) else if (
      { record_ = RecordDecl } = Decl;
      RecordDefn = cxx1.RecordDefinition { declaration = RecordDecl }
    ) then (
      { defn = { record_ = RecordDefn } } = Entity
    ) else if (
      { enum_ = EnumDecl } = Decl;
      EnumDefn = cxx1.EnumDefinition { declaration = EnumDecl }
    ) then (
      { defn = { enum_ = EnumDefn } } = Entity
    ) else if (
      { namespace_ = NamespaceDecl } = Decl;
      NamespaceDefn = cxx1.NamespaceDefinition { declaration = NamespaceDecl }
    ) then (
      { defn = { namespace_ = NamespaceDefn } } = Entity
    ) else if (
      { objcContainer = ContDecl } = Decl;
      ContainerDefn = cxx1.ObjcContainerDefinition { declaration = ContDecl }
    ) then (
      { defn = { objcContainer = ContainerDefn } } = Entity
    ) else if (
      { objcMethod = MethDecl } = Decl;
      MethDefn = cxx1.ObjcMethodDefinition MethDecl
    ) then (
      { defn = { objcMethod = MethDefn } } = Entity
    ) else (
      { decl = Decl } = Entity
    )

# Finding entities' locations

predicate CxxEntityLocation:
  {
    entity: code.cxx.Entity,
    location: codemarkup.types.Location,
  }
  { Entity , { Name, File, { range = Range }, MSpan } } where
    ( { decl = Decl } = Entity;
      cxx1.DeclarationLocationNameSpan { Decl, Range, Name, _, Span };
      { file = File } = Range;
      { just = Span } = MSpan;
    ) | (
      { defn = Defn } = Entity;
      cxx1.DefToBaseDecl { Defn, Decl };
      cxx1.DeclarationLocationNameSpan { Decl, Range, Name, _, Span };
      { file = File } = Range;
      { just = Span } = MSpan;
    ) | (
      { enumerator = { name = cxx1.Name Name, source = Range } } = Entity;
      { file = File } = Range;
      nothing = MSpan; # we only have the range for enumerators
    );

# Finding references in a file

# Either C++ / ObjC xrefs or C preprocessor xrefs
predicate CxxFileEntityXRefLocations:
  {
    file: src.File,
    xref: codemarkup.types.XRefLocation,
    entity: code.cxx.Entity,
  }
  { File, XRef, Entity } where
    CxxFileEntityFixedXRefLocations { File, XRef, Entity }

# Fixed C++ xrefs only
predicate CxxFileEntityFixedXRefLocations:
  {
    file: src.File,
    xref: codemarkup.types.XRefLocation,
    entity: code.cxx.Entity,
  }
  { File, XRef, Entity } where
    CxxFileEntityTraceFixedXRefLocations { File, _, XRef, Entity }

# Fixed C++ xrefs only, for a specific trace (FileXRefMap)
# (deprecated, use CxxFileEntityXMapFixedXRefLocations)
predicate CxxFileEntityTraceFixedXRefLocations:
  {
    file: src.File,
    trace: cxx1.FileXRefMap,
    xref: codemarkup.types.XRefLocation,
    entity: code.cxx.Entity,
  }
  { File, Trace, XRef, Entity } where
    cxx1.FileXRefMap { file = File, fixed = Fixeds } = Trace;
    cxx1.FileXRefs { Trace, _ };
    cxx1.FixedXRef { XRefTarget, Uses } = Fixeds[..];
    CxxXRefTargetLocation { XRefTarget, Entity, Location };
    Spans = prim.relToAbsByteSpans Uses;
    Span = Spans[..];
    { Location, { span = Span }} = XRef;

# Fixed C++ xrefs, keyed by a specific cxx1.FileXRefs fact
# We will expand any targets as best we can
predicate CxxFileEntityXMapFixedXRefLocations:
  {
    trace: cxx1.FileXRefs,
    xref: codemarkup.types.XRefLocation,
    entity: code.cxx.Entity,
  }
  { Trace, XRef, Entity } where
    cxx1.FileXRefs { xmap = { fixed = Fixeds } } = Trace;
    cxx1.FixedXRef { XRefTarget, Uses } = Fixeds[..];
    CxxXRefTargetLocation { XRefTarget, Entity, Location };
    Spans = prim.relToAbsByteSpans Uses;
    Span = Spans[..];
    { Location, { span = Span }} = XRef;

# Helper to (partially) process XRefTargets to entities and xrefs
predicate CxxXRefTargetLocation:
  {
    target: cxx1.XRefTarget,
    entity: code.cxx.Entity,
    location: codemarkup.types.Location,
  }
  { XRefTarget, Entity, Location } where
    # fixed declaration entities
    ( { declaration = Decl } = XRefTarget;
      cxx1.DeclarationLocationNameSpan { Decl, Range, Name, _, Span };
      { file = TargetFile } = Range;
      { Name, TargetFile, { range = Range }, { just = Span } } = Location;
      CxxResolveDeclarationToEntity { Decl, Entity };
    ) | (
    # or jump through the decl sometimes to a def
      { declaration = Decl } = XRefTarget;
      CxxDeclToDefXRefTargetLocation { Decl, Entity, Location };
    ) | (
    # fixed enumerator entities
      { enumerator = Enum } = XRefTarget;
      { cxx1.Name Name, _, Range } = Enum;
      { file = TargetFile } = Range;
      { Name, TargetFile, { range = Range }, nothing } = Location;
      { enumerator = Enum } = Entity;
    )
    # objSelector -- not an entity type
    # unknown -- just a file and a point in the file tbd
    # indirect -- can't implement due to recursion

#
# Find xrefs associated with decls in this file / trace
#
# For decls, compute unique xrefs to defns
# For defns, compute unique xrefs to decls (decl families)
#
predicate CxxFileEntityTraceDeclToDefXRefLocations:
  {
    file: src.File,
    trace: cxx1.Trace,
    xref: codemarkup.types.XRefLocation,
    entity: code.cxx.Entity,
  }
  { File, Trace, XRef, Entity } where
    Trace = cxx1.Trace { File, cxx1.Declarations Decls, _ };
    Decl = Decls[..];
    cxx1.DeclarationNameSpan { Decl, _, Span };
    # Get xrefs from decl to defn ,and defn back to decl family
    ({ Entity, Location } where
      CxxDeclToDefXRefTargetLocation { Decl, Entity, Location } |
      CxxDefToDeclFamilyXRefTargetLocation { Decl, Entity, Location }
    );
    { Location, { span = Span }} = XRef;

# For a given Decl, it might have a base Defn, build an entity for that
# Iff the target is not the same as the source Decl
predicate CxxDeclToDefXRefTargetLocation:
  {
    decl: cxx1.Declaration,
    entity: code.cxx.Entity,
    location: codemarkup.types.Location
  }
  { Decl1, Entity, Location } where
    code.cxx.DeclToDef { Decl1, Defn };
    cxx1.DefToBaseDecl { Defn , Decl2 };
    Decl1 != Decl2; # Filter out (many) self refs
    cxx1.DeclarationLocationNameSpan { Decl2, Range, Name, _, Span };
    { file = TargetFile } = Range;
    { Name, TargetFile, { range = Range }, { just = Span } } = Location;
    { defn = Defn } = Entity;

# For a given Decl, find any DeclFamily entries
# We will turn these into xrefs
predicate CxxDefToDeclFamilyXRefTargetLocation:
  {
    decl: cxx1.Declaration,
    entity: code.cxx.Entity,
    location: codemarkup.types.Location,
  }
  { Decl1, Entity, Location } where
    cxx1.DeclFamilyOf { decl = Decl1, family = Decl2 };
    Decl1 != Decl2; # filter out many self-refs
    cxx1.DeclarationLocationNameSpan { Decl2, Range, Name, _, Span };
    { file = TargetFile } = Range;
    { Name, TargetFile, { range = Range }, { just = Span } } = Location;
    { decl = Decl2 } = Entity;

# Bulk fetch any declaration locations associated with the xref targets
# Rather than O(n) calls to map over the external target xrefs,
# we can bulk-fetch all locations in one go, saving a lot of calls
predicate CxxFileEntityXMapVariableXRefDeclLocations:
  {
    trace: cxx1.FileXRefs,
    source: cxx1.Declaration, # used to identify the source
    location: codemarkup.types.Location,
  }
  { Trace, Decl, Location } where
    cxx1.FileXRefs { externals = Targets } = Trace;
    { declaration = Decl } = Targets[..];
    cxx1.DeclarationLocationNameSpan { Decl, Range, Name, _, Span };
    { file = TargetFile } = Range;
    { Name, TargetFile, { range = Range }, { just = Span } } = Location;

# Bulk fetch any definitions associated with the external decls in the filexrefs
predicate CxxFileEntityXMapVariableXRefDeclToDefs:
  {
    trace: cxx1.FileXRefs,
    source: cxx1.Declaration, # used to identify the source
    entity: code.cxx.Entity, # enough stuff to build a xreflocation
    location: codemarkup.types.Location,
  }
  { Trace, Decl, Entity, Location } where
    cxx1.FileXRefs { externals = Targets } = Trace;
    # decls
    # todo:  more cases: recursion: indirect xrefs could be decls too
    { declaration = Decl } = Targets[..];
    CxxDeclToDefXRefTargetLocation { Decl, Entity, Location };

#
# Language entity uses
#

predicate CxxEntityUses:
  {
    target: code.cxx.Entity,
    file: src.File,
    span: src.ByteSpan,
  }
  ({ { decl = Decl }, File, Span } where
    cxx1.DeclFamilyOf { Decl, D };
    cxx1.TargetUses { { declaration = D }, File, Uses };
    Spans = prim.relToAbsByteSpans Uses;
    Span = Spans[..]) |
  ({ { defn = Defn }, File, Span } where
    cxx1.DefToBaseDecl { Defn, DeclBase };
    cxx1.DeclFamilyOf { DeclBase, D };
    cxx1.TargetUses { { declaration = D }, File, Uses };
    Spans = prim.relToAbsByteSpans Uses;
    Span = Spans[..]) |
  ({ { enumerator = E }, File, Span } where
    cxx1.TargetUses { { enumerator = E }, File, Uses };
    Spans = prim.relToAbsByteSpans Uses;
    Span = Spans[..])

#
# Language-specific symbol info
#

predicate CxxEntityKind:
  {
    entity: code.cxx.Entity,
    kind: codemarkup.types.SymbolKind,
  }
  { CxxD, Kind } where
    ({ decl = Decl } = CxxD;
     CxxDeclKind { Decl, Kind }) |
    ({ enumerator = _ } = CxxD; Enumerator = Kind) |
    ({ defn = Defn } = CxxD;
     cxx1.DefToBaseDecl { Defn, Decl };
     CxxDeclKind { Decl, Kind } )

predicate CxxDeclKind:
  {
    decl: cxx1.Declaration,
    kind: codemarkup.types.SymbolKind,
  }
  { Decl, Kind } where
    ({ function_ = { method = MM } } = Decl;
       ({ just = _ } = MM; Method = Kind) |
       (nothing = MM; Function = Kind)
    ) |
    (
      ({ enum_ = _ } = Decl; Enum = Kind ) |
      ({ namespace_ = _ } = Decl; Namespace = Kind ) |
      ({ objcMethod = _ } = Decl; Method = Kind ) |
      ({ objcProperty = _ } = Decl; Property = Kind ) |
      ({ typeAlias = _ } = Decl; Type = Kind ) | # typedef or using
      ({ usingDeclaration = _ } = Decl; Namespace = Kind ) |
      ({ variable = _ } = Decl; Variable = Kind ) |
      # TODO: objc containers, using directives
      ({ record_ = R } = Decl;
        ({ kind = { struct_ = _ } } = R; Struct = Kind ) |
        ({ kind = { class_ = _ } } = R; Class_ = Kind ) |
        ({ kind = { union_ = _ } } = R; Union = Kind )
      )
    )

predicate CxxEntityInfo:
  {
    entity: code.cxx.Entity,
    info: codemarkup.types.SymbolInfo,
  }
  { CxxD, Info } where
    ({ decl = Decl } = CxxD;
     CxxDeclInfo { Decl, Info }) |
    ({ enumerator = _ } = CxxD;
     { kind = Enumerator, isAbstract = false } = Info) |
    ({ defn = Defn } = CxxD;
     cxx1.DefToBaseDecl { Defn, Decl };
     CxxDeclInfo { Decl, Info }; )

predicate CxxDeclInfo:
  {
    decl: cxx1.Declaration,
    info: codemarkup.types.SymbolInfo,
  }
  { Decl, { kind = Kind, isAbstract = Abs } } where
    CxxDeclKind { decl = Decl, kind = Kind };
    if (Method = Kind) then (
      ({ function_ = { method = { just = M }}} = Decl;
         { isVirtual = Abs } = M
      ) |
      (
        { objcMethod = _ } = Decl;
        false = Abs;
      )
    ) else (
      false = Abs
    )

#
# Annotations
#

predicate CxxAnnotation:
  {
    entity: code.cxx.Entity,
    anns: code.cxx.Annotations,
  }
  {{ decl = Decl }, { attributes = As }} where
    { function_ = F } = Decl;
    ( cxx1.FunctionDeclAttribute { F, A }; As=[A] ) |
    (!cxx1.FunctionDeclAttribute { F, _ }; As=[]:[cxx1.Attribute]);

#
# Visibility
#

predicate CxxVisibility:
  {
    entity: code.cxx.Entity,
    visibility: codemarkup.types.Visibility,
  }
  { {decl=Decl}, Access } where
    (
      ({ function_ = {name={scope=Scope} } } = Decl) |
      ({ enum_ = {name={scope=Scope} } } = Decl) |
      ({ record_ = {name={scope=Scope} } } = Decl)
    );
    ({recordWithAccess={access=Access}}=Scope;{}:{})

#
# Relations
#

# The child `extends` parent relationship
predicate CxxExtendsParentEntity :
  {
    child : code.cxx.Entity,
    parent : code.cxx.Entity
  }
  { Child, Parent } where
    ( { decl = Decl } = Child;
      CxxDeclarationExtendsParent { Decl, Parent }
    ) | (
      { defn = Defn } = Child;
      CxxDefinitionExtendsParent { Defn, Parent }
    )

# child extends parent for definitions
#
# - records (structs, classses)
# - methods
# - objC things
#
predicate CxxDefinitionExtendsParent:
  {
    child : code.cxx.Definition,
    parent : code.cxx.Entity
  }
  { Child, Parent } where
    ( { record_ = C } = Child;
      cxx1.RecordDefinition { bases = BS } = C;
      cxx1.RecordBase { base = P } = BS[..];
      CxxResolveDeclarationToEntity { { record_ = P }, Parent }
    ) | (
      { function_ = _ } | { objcContainer = _ } = Child;
      cxx1.DefToBaseDecl { Child, C };
      CxxDeclarationExtendsParent { C, Parent }
    )

predicate CxxDeclarationExtendsParent:
  {
    child : cxx1.Declaration,
    parent : code.cxx.Entity
  }
  { Child, Parent } where
    ( { record_ = _ } = Child;
      code.cxx.DeclToDef { Child, Defn };
      CxxDefinitionExtendsParent { Defn, Parent }
    ) | (
      { function_ = C } = Child;
      cxx1.MethodOverrides { C, P };
      CxxResolveDeclarationToEntity { { function_ = P }, Parent }
    ) | (
      { objcContainer = C } = Child;
      cxx1.ObjcImplements { C, P } | cxx1.ObjcContainerBase{ C, P };
      CxxResolveDeclarationToEntity { { objcContainer = P }, Parent }
    )

# The parent `extended by` child relationship
predicate CxxExtendsChildEntity :
  {
    parent: code.cxx.Entity,
    child: code.cxx.Entity
  }
  { Parent, Child } where
    ( { decl = Decl } = Parent;
      CxxDeclarationExtendsChild { Decl, Child }
    ) | (
      { defn = Defn } = Parent;
      CxxDefinitionExtendsChild { Defn, Child }
    )

predicate CxxDefinitionExtendsChild:
  {
    parent: code.cxx.Definition,
    child: code.cxx.Entity
  }
  { Parent, Child } where
    cxx1.DefToBaseDecl { Parent, Decl };
    CxxDeclarationExtendsChild { Decl, Child }

predicate CxxDeclarationExtendsChild:
  {
    parent: cxx1.Declaration,
    child: code.cxx.Entity
  }
  { Parent, Child } where
    ( { record_ = P } = Parent;
      cxx1.RecordDerived { P, C };
      CxxResolveDeclarationToEntity { { record_ = C }, Child }
    ) | (
      { function_ = P } = Parent;
      cxx1.MethodOverridden { P, C };
      CxxResolveDeclarationToEntity { { function_ = C }, Child }
    ) | (
      { objcContainer = P } = Parent;
      cxx1.ObjcInterfaceToImplementation { P, C } |
        cxx1.ObjcContainerInheritance { P, C };
      CxxResolveDeclarationToEntity { { objcContainer = C }, Child }
    )

# The child of parent relationship
#
# works for most things including
#
# - enumerators
# - nested records
# - namespaces
#
predicate CxxContainsParentEntity :
  {
    child: code.cxx.Entity,
    parent: code.cxx.Entity
  }
  { Child, Parent } where
    ( { decl = Decl } = Child;
      CxxDeclarationContainsParent { Decl, Parent };
    ) | (
      { defn = Defn } = Child;
      cxx1.DefToBaseDecl { Defn, C };
      CxxDeclarationContainsParent { C, Parent }
    ) | (
     { enumerator = E } = Child;
     cxx1.EnumeratorInEnum { E, Enum };
     { defn = { enum_ = Enum } } = Parent
    )

#
# we can find parents via the cxx1.Scope of the child
#
predicate CxxDeclarationContainsParent:
  {
    child: cxx1.Declaration,
    parent: code.cxx.Entity
  }
  { Child, Parent } where
    # most C++ things: functions, records, variables:
    ( cxx1.DeclarationScope { Child, Scope };
      # and then the parent can be a namespace, record or function
      ( { recordWithAccess = R } = Scope;
        { record = QName } = R;
        P = cxx1.RecordDeclaration { name = QName };
        CxxResolveDeclarationToEntity { { record_ = P }, Parent }
      ) | (
        { namespace_ = ParentNS } = Scope;
        P = cxx1.NamespaceDeclaration { name = ParentNS };
        CxxResolveDeclarationToEntity { { namespace_ = P }, Parent }
      )
    ) | (
      { objcMethod = Decl } = Child;
      cxx1.ObjcMethodDeclaration { container = ContainerId } = Decl;
      P = cxx1.ObjcContainerDeclaration { id = ContainerId };
      CxxResolveDeclarationToEntity { { objcContainer = P }, Parent }
    ) | (
      { objcProperty = Decl } = Child;
      cxx1.ObjcPropertyDeclaration { container = ContainerId } = Decl;
      P = cxx1.ObjcContainerDeclaration { id = ContainerId };
      CxxResolveDeclarationToEntity { { objcContainer = P }, Parent }
    # todo: can objcContainerDecls have containers?
    ) | (
      # namespaces have direct parents
      { namespace_ = { name = NQN }  } = Child;
      { parent = { just = ParentNQN } } = NQN;
      P = cxx1.NamespaceDeclaration { name = ParentNQN };
      CxxResolveDeclarationToEntity { { namespace_ = P }, Parent }
    )



#
# The parent `contains` child relationship
#
predicate CxxContainsChildEntity :
  {
    parent: code.cxx.Entity,
    child: code.cxx.Entity
  }
  { Parent, Child } where
    ( { decl = Decl} = Parent;
      CxxDeclarationContainsChild { Decl, Child }
    ) | (
      { defn = Defn } = Parent;
      CxxDefinitionContainsChild { Defn, Child }
    )

# Lookup container child for definition occurences
#
# - records
# - objc containers
# - namespaces
# - enums
#
predicate CxxDefinitionContainsChild:
  {
    parent: code.cxx.Definition,
    child: code.cxx.Entity
  }
  { Parent, Child } where
    ( { record_ = Defn } = Parent;
      cxx1.RecordDefinition { members = cxx1.Declarations Decls } = Defn;
      Decl = Decls[..];
      CxxResolveDeclarationToEntity { Decl, Child }
    ) | (
      { objcContainer = Defn } = Parent;
      cxx1.ObjcContainerDefinition { members = cxx1.Declarations Decls } = Defn;
      Decl = Decls[..];
      CxxResolveDeclarationToEntity { Decl, Child }
    ) | (
      # namespace member facts are all empty, so instead lookup by scope
      { namespace_ = Defn } = Parent;
      cxx1.NamespaceDefinition { declaration = Decl } = Defn;
      CxxNamespaceDeclarationContainsChild { Decl, Child }
    ) | (
      { enum_ = Defn } = Parent;
      cxx1.EnumDefinition { enumerators = Es } = Defn;
      E = Es[..];
      { enumerator = E } = Child
    )
      # we could add params of methods and functions
      # { function_ = Defn } = Container;
      # ( { objcMethod = Defn } = Container ) |

      # these do not have children
      # ( { variable = Defn } = Container ) |

# Lookup container children via decl goes via its definition
predicate CxxDeclarationContainsChild:
  {
    parent: cxx1.Declaration,
    child: code.cxx.Entity
  }
  { Parent, Child } where
    ( { namespace_ = Decl } = Parent;
      CxxNamespaceDeclarationContainsChild { Decl, Child }
    ) | (
      ({ record_ = _ } | { objcContainer = _ } | { enum_ = _ } ) = Parent;
      code.cxx.DeclToDef { Parent, Defn };
      CxxDefinitionContainsChild { Defn, Child }
    )

# Namespaces contain children with the namespace as its scope
# these can be quite large for common namespaces shared widely
predicate CxxNamespaceDeclarationContainsChild:
  {
    parent: cxx1.NamespaceDeclaration,
    child: code.cxx.Entity
  }
  { Decl, Child } where
    { name = NSQName } = Decl;
    Scope = cxx1.Scope { namespace_ = NSQName };
    ( Q = cxx1.QName { name = _, scope = Scope }; # n.b. not left prefixed!
        (
          code.cxx.Entity { decl = { record_ = { name = Q }}}  |
          code.cxx.Entity { decl = { variable = { name = Q }}} |
          code.cxx.Entity { decl = { typeAlias = { name = Q }}} |
          code.cxx.Entity { decl = { enum_ = { name = Q }}}
        )
    ) | (
        D = cxx1.FunctionDeclaration { name = { scope = Scope } };
        code.cxx.Entity { decl = { function_ = D } }
    ) = Child;

# comment blocks across declaration families
predicate CxxEntityDocumentation :
  {
    entity : code.cxx.Entity,
    file : src.File,
    span : src.ByteSpan,
  }
  { E, F, S } where
    ( { decl = Decl } = E;
      cxx1.DeclFamilyOf { Decl, DeclFam };
      cxx1.DeclarationComment{ declaration = DeclFam, file = F, span = S }
    ) | (
      { defn = Defn } = E;
      cxx1.DefToBaseDecl { Defn, DeclBase };
      cxx1.DeclFamilyOf { DeclBase, Decl };
      cxx1.DeclarationComment{ declaration = Decl, file = F, span = S }
    )

}

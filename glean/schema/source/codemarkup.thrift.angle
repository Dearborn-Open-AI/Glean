# Copyright (c) Facebook, Inc. and its affiliates.

schema codemarkup.thrift.1 {
import src.1
import code.20
import codemarkup.types.1
import thrift.4

#
# Resolving locations to entities
#

predicate ThriftResolveLocation:
  {
    location: codemarkup.types.Location,
    entity: code.Entity,
  }
  { { Name, FileS, { range = Range }, nothing }, Entity } where
    FileT = thrift.File FileS;
    thrift.FileDeclaration { FileT, Declaration };
    thrift.DeclarationNameSpan { Declaration, Identifier, FileT, {LB, CB, LE, CE} };
    Identifier = thrift.Identifier Name;
    { file = FileS, lineBegin = LB, columnBegin = CB, lineEnd = LE, columnEnd = CE } = Range;
    Entity = code.Entity { thrift = Declaration };

#
# Finding entities' locations
#

predicate ThriftEntityLocation:
  {
    entity: code.Entity,
    location: codemarkup.types.Location,
  }
 { Entity, { Name, FileS, { range = Range }, nothing } } where
    thrift.DeclarationNameSpan { Declaration, Identifier, FileT, Loc };
    Entity = code.Entity { thrift = Declaration };
    Identifier = thrift.Identifier Name;
    FileT = thrift.File FileS;
    { LB, CB, LE, CE } = Loc;
    { lineBegin = LB, columnBegin = CB, lineEnd = LE, columnEnd = CE } = Range;

#
# Finding references in a file
#

predicate ThriftFileEntityXRefLocations:
  {
    file: src.File,
    xref: codemarkup.types.XRefLocation,
    entity: code.Entity,
  }
 { File, { target = TargetLocation, source = { range = SourceRange } }, Entity } where
  FileT = thrift.File File;
  thrift.FileXRefs { file = FileT, xrefs = XRefs };
  { { startLine = LB, startCol = CB, endLine = LE, endCol = CE }  , XRefTarget } = XRefs[..];
  { file = File, lineBegin = LB, columnBegin = CB, lineEnd = LE, columnEnd = CE } = SourceRange;
  thrift.DeclarationNameSpan { decl = XRefTarget,
                               name = thrift.Identifier Name,
                               file = TargetFileT,
                               span = { TLB, TCB, TLE, TCE } };
  TargetFileT = thrift.File TargetFile;
  TargetRange = {TargetFile, TLB, TCB, TLE, TCE };
  { Name, TargetFile, { range = TargetRange }, nothing } = TargetLocation;
  Entity = code.Entity { thrift = XRefTarget };

}

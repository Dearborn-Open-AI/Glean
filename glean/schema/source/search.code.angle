# Copyright (c) Facebook, Inc. and its affiliates.

schema search.code.16 {
import code.24
import code.cxx.4
import code.hack.4
import code.python.1
import cxx1.5
import flow.3
import hack.6
import erlang.1
import hs.2
import lsif.2
import pp1.1
import python.4
import rust.1
import search.cxx.5
import search.erlang.4
import search.hack.7
import search.hs.9
import search.python.1
import thrift.7

#
# public API
#

# global symbol search with language constraint
predicate SearchByNameAndLanguage:
  {
    name: string,
    language: code.Language,
    entity: code.Entity,
  }
  { Name, Language, Entity } where
    ( Hack = Language; HackSearchByName { Name, Entity } ) |
    ( Python = Language; PythonSearchByName { Name, Entity } ) |
    ( JavaScript = Language; FlowSearchByName { Name, Entity } ) |
    ( Cpp = Language; CxxSearchByName { Name, Entity } ) |
    ( Haskell = Language; HsSearchByName { Name, Entity } ) |
    ( Erlang = Language; ErlangSearchByName { Name, Entity } ) |
    ( Rust = Language; RustSearchByName { Name, Entity } );
    code.EntityLanguage { Entity, Language }; # double check the entity lang

# global symbol search normalized to lower case
predicate SearchByLowerCaseNameAndLanguage:
  {
    name: string,
    language: code.Language,
    entity: code.Entity,
  }
  { Name, Language, Entity } where
    ( Hack = Language; HackSearchByLowerCaseName { Name, Entity } ) |
    ( Python = Language; PythonSearchByLowerCaseName { Name, Entity } ) |
    ( JavaScript = Language; FlowSearchByLowerCaseName { Name, Entity } ) |
    ( Cpp = Language; CxxSearchByLowerCaseName { Name, Entity } ) |
    ( Haskell = Language; HsSearchByLowerCaseName { Name, Entity } ) |
    ( Erlang = Language; ErlangSearchByLowerCaseName { Name, Entity } ) |
    ( Rust = Language; RustSearchByLowerCaseName { Name, Entity } );
    code.EntityLanguage { Entity, Language }; # double check the entity lang

# scoped search (experimental!)

predicate SearchByScope:
  {
    name: string,
    scope: [string],
    language: code.Language,
    entity: code.Entity,
  }
  { Name, Scope, Language, Entity } where
    ( Cpp = Language; CxxSearchByScope { Name, Scope, Entity } ) |
    ( Hack = Language; HackSearchByScope { Name, Scope, Entity } ) |
    ( Python = Language; PythonSearchByScope { Name, Scope, Entity } );
    code.EntityLanguage { Entity, Language };

# lower case by scope
predicate SearchByLowerCaseScope:
  {
    name: string,
    scope: [string],
    language: code.Language,
    entity: code.Entity,
  }
  { Name, Scope, Language, Entity } where
    ( Cpp = Language; CxxSearchByLowerCaseScope { Name, Scope, Entity } ) |
    ( Hack = Language; HackSearchByLowerCaseScope { Name, Scope, Entity } ) |
    ( Python = Language; PythonSearchByLowerCaseScope { Name, Scope, Entity } );
    code.EntityLanguage { Entity, Language };

# global entity search by identifier name (or prefix)
# (deprecated)
predicate SearchByName:
  {
    name: string,
    entity: code.Entity,
  }
  { Name, Entity } where
  PythonSearchByName { Name, Entity } |
  HackSearchByName { Name, Entity } |
  FlowSearchByName { Name, Entity } |
  CxxSearchByName { Name, Entity } |
  HsSearchByName { Name, Entity } |
  ErlangSearchByName { Name, Entity } |
  LsifSearchByName { Name, Entity } |
  RustSearchByName { Name, Entity }

# same as SearchByName but assumes `name` is lowercase. Useful for
# case-insensitive search
# (deprecated)
predicate SearchByLowerCaseName:
  {
    name: string,
    entity: code.Entity,
  }
  { Name, Entity } where
  PythonSearchByLowerCaseName { Name, Entity } |
  HackSearchByLowerCaseName { Name, Entity } |
  FlowSearchByLowerCaseName { Name, Entity } |
  CxxSearchByLowerCaseName { Name, Entity } |
  HsSearchByLowerCaseName { Name, Entity } |
  ErlangSearchByLowerCaseName { Name, Entity } |
  LsifSearchByLowerCaseName { Name, Entity } |
  RustSearchByLowerCaseName { Name, Entity };

#
# Language-specific identifier search
#

predicate HackSearchByName:
  {
    name: string,
    entity: code.Entity,
  }
  { NameStr, Entity } where
    Name = hack.Name NameStr;
    HackSearchByNameWithName { Name, Entity };

predicate HackSearchByLowerCaseName:
  {
    name: string,
    entity: code.Entity,
  }
  { Name, Entity } where
    hack.NameLowerCase { Name, NameCase };
    HackSearchByNameWithName { NameCase, Entity };

# Helper to reuse the Name fact found previously
predicate HackSearchByNameWithName:
  {
    name : hack.Name,
    entity : code.Entity
  }
  { Name, { hack = { decl = Decl }}} where
    search.hack.SearchByName { name = Name, decl = Decl };

predicate HackSearchByScope:
  {
    name: string,
    scope: [string],
    entity: code.Entity,
  }
  { NameStr, Query, Entity } where
    Name = hack.Name NameStr;
    Insensitive = false;
    HackSearchByScopeWithName { Name, Insensitive, Query, Entity }

predicate HackSearchByLowerCaseScope:
  {
    name: string,
    scope: [string],
    entity: code.Entity,
  }
  { NameStr, Query, Entity} where
    hack.NameLowerCase { NameStr, Name };
    Insensitive = true;
    HackSearchByScopeWithName { Name, Insensitive, Query, Entity }

# private helper to share name facts between the calsl
#
# by always using the matched namespace we make prevent scope wildcards
# e.g. C\count won't match Test\C\count
# we could try to interpret prefix mode this way though
#
predicate HackSearchByScopeWithName:
  {
    name: hack.Name,
    insenstive : bool,
    scope: [string],
    entity: code.Entity,
  }
  { Name, Insensitive, Scope, { hack = { decl = Decl }}} where
    if ( [] = Scope; true | false = Insensitive )
      then (
        ( # exactly global declarations
          search.hack.SearchInNamespace { Name, nothing, Decl }
        ) | (
          # or global namespace alias children ("FlibSL\Vec" or "C" work)
          hack.GlobalNamespaceAlias { Name, NSQName };
          NSDecl = hack.NamespaceDeclaration { name = NSQName };
          { namespace_ = NSDecl } = Decl
        ) | (
          # or children of auto-imported namespaces (just HH for now)
          search.hack.HackInAutoImportedNamespace { Name, HHNSQName };
          search.hack.SearchInNamespace { Name, HHNSQName, Decl }
        )
      )
      else (
        # search within specific context
        search.hack.QueryToScopeCase { Scope, Insensitive, ScopeName, ScopeNamespace };
        search.hack.SearchInContext { Name, ScopeName, ScopeNamespace, Decl }
      );

predicate PythonSearchByName:
  {
    name: string,
    entity: code.Entity,
  }
  { NameStr, Entity } where
    LocalName = python.LocalName NameStr;
    PythonSearchByLocalNameFact { LocalName, Entity }

predicate PythonSearchByLowerCaseName:
  {
    name: string,
    entity: code.Entity,
  }
  { NameStr, Entity } where
    python.LocalNameLowerCase { NameStr, LocalName };
    PythonSearchByLocalNameFact { LocalName, Entity }

# helper to avoid repeatedly doing string search
predicate PythonSearchByLocalNameFact:
  {
    name: python.LocalName,
    entity: code.Entity,
  }
  { LocalName, { python = { decl = Decl }}} where
    python.SearchByLocalName { LocalName, Decl };
    python.NonImportDeclaration Decl;

predicate PythonSearchByScope:
  {
    name: string,
    scope: [string],
    entity: code.Entity,
  }
  { NameStr, Query, Entity } where
    LocalName = python.LocalName NameStr;
    Insensitive = false;
    PythonSearchByScopeWithName { LocalName, Insensitive, Query, Entity }

predicate PythonSearchByLowerCaseScope:
  {
    name: string,
    scope: [string],
    entity: code.Entity,
  }
  { NameStr, Query, Entity} where
    python.LocalNameLowerCase { NameStr, LocalName };
    Insensitive = true;
    PythonSearchByScopeWithName { LocalName, Insensitive, Query, Entity }

# Private: search by scope anchored on a local_name fact
predicate PythonSearchByScopeWithName:
  {
    name: python.LocalName,
    insensitive : bool,
    scope: [string],
    entity: code.Entity,
  }
  { LocalName, Insensitive, Scope, { python = { decl = Decl }}} where
    LocalName = python.LocalName NameStr;
    search.python.QueryToScopeCase { Scope, Insensitive, MSName };
    SName = python.SName { python.Name NameStr, MSName };
    python.DeclarationWithSName { SName, Decl };
    python.NonImportDeclaration Decl;

predicate FlowSearchByName:
  {
    name: string,
    entity: code.Entity,
  }
  { NameStr, Entity } where
    ( Name = flow.Name NameStr;
      FlowSearchByNameWithFact { Name, Entity }
    ) | (
      flow.SearchByNameModule { NameStr, Mod };
      { flow = { module_ = Mod } } = Entity
    )

predicate FlowSearchByLowerCaseName:
  {
    name: string,
    entity: code.Entity,
  }
  { NameStr, Entity } where
    flow.NameLowerCase { NameStr, Name };
    FlowSearchByNameWithFact { Name, Entity }

# helper to share Name fact in the lower case scenario
#
# Similar to C++ and Python, we don't include import declarations
# in search results, as they are too noisy
#
predicate FlowSearchByNameWithFact:
  {
    name: flow.Name,
    entity: code.Entity
  }
  { Name, { flow = { decl = Decl } } } where
    flow.SearchByName { Name, Decl };
    # filter out import declarations
    ({ localDecl = LocalDecl } = Decl;
     !flow.ImportDeclaration { declaration = LocalDecl }
    ) | (
     { typeDecl = TypeDecl } = Decl;
     !flow.TypeImportDeclaration { typeDeclaration = TypeDecl }
    ) | (
     { memberDecl = _ } = Decl;
    )

predicate CxxSearchByName:
  {
    name: string,
    entity: code.Entity,
  }
  { NameStr, Entity } where
    Name = cxx1.Name NameStr;
    CxxSearchByNameAndScopeFact { name = Name, entity = Entity };

predicate CxxSearchByLowerCaseName:
  {
    name: string,
    entity: code.Entity,
  }
  { NameStr, Entity } where
  cxx1.DeclByName { name_lowercase = NameStr, ident = Id };
  (
    { name = Name } = Id;
    CxxSearchByNameAndScopeFact { name = Name, entity = Entity };
  ) | (
    { selector = Sel } = Id;
    search.cxx.SearchBySelector { Sel, Ent };
    { cxx = Ent } = Entity;
  )

predicate CxxSearchByScope:
  {
    name: string,
    scope: [string],
    entity: code.Entity,
  }
  { NameStr, Query, Entity } where
    search.cxx.QueryToScope { Query, Scope };
    Name = cxx1.Name NameStr;
    CxxSearchByNameAndScopeFact { Name, Scope, Entity }

predicate CxxSearchByLowerCaseScope:
  {
    name: string,
    scope: [string],
    entity: code.Entity,
  }
  { NameStr, Query, Entity } where
    cxx1.DeclByName { name_lowercase = NameStr, ident = { name = Name } };
    search.cxx.QueryToScope { Query, Scope };
    CxxSearchByNameAndScopeFact { Name, Scope, Entity };

predicate CxxSearchByNameAndScopeFact:
  {
    name: cxx1.Name,
    scope : cxx1.Scope,
    entity : code.Entity
  }
  { Name, Scope, { cxx = DefnE }} where
    search.cxx.SearchByNameAndScope { Name, Scope, Ent };
    # I think we can avoid searching through decl families since we're
    # searching all matches anyway
    if ({ decl = Decl } = Ent) # just definitions , not decls
      then ( search.cxx.DeclIsDefn { Decl, Defn }; { defn = Defn } = DefnE )
      else ( Ent = DefnE );

predicate HsSearchByName:
  {
    name: string,
    entity: code.Entity,
  }
  { Name, { hs = E } } where
  search.hs.SearchByName { name = Name, entity = E }

predicate HsSearchByLowerCaseName:
  {
    name: string,
    entity: code.Entity,
  }
  { Name, Entity } where
  (
    hs.ModuleNameLowerCase { Name, hs.ModuleName NameCase } |
    hs.FunctionNameLowerCase { Name, hs.FunctionName NameCase } |
    hs.DefinitionNameLowerCase { Name, hs.DefinitionName NameCase } |
    hs.ClassNameLowerCase { Name, hs.ClassName NameCase }
  );
  HsSearchByName { NameCase, Entity };

predicate ErlangSearchByName:
  {
    name: string,
    entity: code.Entity,
  }
  { Name, { erlang = E } } where
  search.erlang.SearchByName { name = Name, entity = E }

predicate ErlangSearchByLowerCaseName:
  {
    name: string,
    entity: code.Entity,
  }
  { Name, Entity } where
  erlang.NameLowerCase { Name, NameCase };
  ErlangSearchByName { NameCase, Entity }

predicate LsifSearchByName:
  {
    name: string,
    entity: code.Entity,
  }
  { Name, { lsif = Entity } } where
  lsif.SearchByName { lsif.Name Name, Entity }

predicate LsifSearchByLowerCaseName:
  {
    name: string,
    entity: code.Entity,
  }
  { Name, { lsif = Entity } } where
  lsif.NameLowerCase { Name, NameCase };
  lsif.SearchByName { NameCase, Entity }

predicate RustSearchByName:
  {
    name: string,
    entity: code.Entity
  }
  { NameStr, { rust = { definition = Entity } } } where
    Name = rust.Name NameStr;
    rust.SearchByName { Name, Entity }

predicate RustSearchByLowerCaseName:
  {
    name: string,
    entity: code.Entity
  }
  { Name, { rust = { definition = Entity } } } where
    rust.NameLowerCase { Name, NameCase };
    rust.SearchByName { NameCase, Entity }

}

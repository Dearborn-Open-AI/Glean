# Copyright (c) Facebook, Inc. and its affiliates.

schema search.cxx.5 {
import cxx1.5
import code.cxx.4

# Declarations with no scope
predicate GlobalDeclarationWithName :
  {
    name : cxx1.Name,
    decl : cxx1.Declaration,
  }
  { N, D }
  where
  D =
    (
      cxx1.Declaration
        {
          objcContainer =
            {
              id =
                { protocol = N } |
                { interface_ = N } |
                { categoryInterface = { className = N } } |
                { extensionInterface = N } |
                { implementation = N } |
                { categoryImplementation = { className = N } }
            }
        }
    ) |
    (cxx1.Declaration { objcProperty = { name = N }})

# Find direct definition entities for various declaration types
predicate DeclIsDefn :
  {
    decl : cxx1.Declaration,
    defn : code.cxx.Definition
  }
  { Decl, Defn } where
    ({ record_ = D } = Decl;
     { record_ = cxx1.RecordDefinition { declaration = D } } = Defn ) |
    ({ function_ = D } = Decl;
     { function_ = cxx1.FunctionDefinition { declaration = D } } = Defn) |
    ({ enum_ = D } = Decl;
     { enum_ = cxx1.EnumDefinition { declaration = D } } = Defn) |
    # global variables only?
    ({ variable = D } = Decl;
     cxx1.VariableDeclaration { kind = { global_ = { definition = true }}} = D;
     { variable = D } = Defn) |
    ({ objcMethod = D } = Decl;
     { objcMethod = cxx1.ObjcMethodDefinition D } = Defn ) |
    ({ objcContainer = D } = Decl;
     { objcContainer = cxx1.ObjcContainerDefinition { declaration = D }} = Defn)

predicate SearchByNameAndScope :
  {
    name : cxx1.Name,
    scope : cxx1.Scope,
    entity : code.cxx.Entity,
  }
  {N, Scope, Entity} where
    (
      # Several things use QName, so share the query for it
      Q = cxx1.QName { name = N, scope = Scope };
      Entity =
        code.cxx.Entity { decl = { record_ = { name = Q }}}  |
        code.cxx.Entity { decl = { variable = { name = Q }}} |
        code.cxx.Entity { decl = { typeAlias = { name = Q }}} |
        code.cxx.Entity { decl = { enum_ = { name = Q }}};
    ) | (
      D = cxx1.FunctionDeclaration
        { name = { name = { name = N }, scope = Scope } };
      Entity = code.cxx.Entity { decl = { function_ = D } }
    ) | (
      GlobalDeclarationWithName { N, Decl };
      Entity = code.cxx.Entity { decl = Decl };
      { global_ = {} } = Scope;
    ) | (
      E = cxx1.Enumerator { name = N, enumeration = T };
      { name = QN, isScoped = Scoped } = T;
      (
        false = Scoped;
        { scope = Scope } = QN
      ) | (
        true = Scoped;
        { recordWithAccess = { record = QN, access = Public }} = Scope
      );
      Entity = code.cxx.Entity { enumerator = E };
    )

predicate SearchByScope :
  {
    scope : cxx1.Scope,
    entity : code.cxx.Entity,
  }
  {Scope, Entity} where
    (
        {namespace_= QName } = Scope;
        NS = cxx1.NamespaceDeclaration { name = QName };
        Entity = code.cxx.Entity { decl = { namespace_ = NS } };
    )

predicate SearchBySelector :
  {
    selector : cxx1.ObjcSelector,
    entity : code.cxx.Entity,
  }
  { Selector, Ent } where
  Ent = code.cxx.Entity { decl = { objcMethod = { selector = Selector }}}

# Easy way to query for TargetUses for an Entity. Note this doesn't
# handle the `defn` alternative of `code.cxx.Entity`, but the
# call site of this predicate ('Glean.Search.Search') always uses it
# with declarations and not definitions.
predicate EntityUses :
  {
    entity : code.cxx.Entity,
    uses : cxx1.TargetUses
  }
  { E, U } where
    (
      code.cxx.Entity { decl = D } = E;
      U = cxx1.TargetUses { target = { declaration = D } }
    ) | (
      code.cxx.Entity { enumerator = En } = E;
      U = cxx1.TargetUses { target = { enumerator = En } }
    )

#
# Generating scope values for queries
#
# We could play games with holes in the namespace query, or anonymous
# segments
#

# Compile search query tuples into scope facts
predicate QueryToScope :
  {
    query : [string],
    scope : cxx1.Scope
  }
  { Query, Scope } where
    # global wild
    ( [] = Query;
     { global_ = _ } ) |
    # namespace scope
    ( QueryToNSQName { Query, NSQName };
      { namespace_ = NSQName } ) |
    ( QueryToQName { Query, QName };
      { recordWithAccess = { record = QName,
          access = Public | Protected | Private } } # avoid wild card bind error
    ) = Scope;

#
# we don't have recursion, so have to flatten tuples manually.
# Manual codegen below...
#

# compile to generic scoped qname
predicate QueryToQName:
  {
    query : [string],
    scope : cxx1.QName
  }
  { Query, QName } where
    # zero tuple
    ( [] = Query;
      cxx1.QName { name = "", scope = { global_ = {} } } ) |
    # one tuple
    ( [A] = Query;
      AName = cxx1.Name A;
      { name = AName } ) |
    # two tuple. we branch back to scope cases
    ( [B,A] = Query;
      AName = cxx1.Name A;
      BName = cxx1.Name B;
      { name = AName, scope =
        { namespace_ = { name = { just = BName } } } |
        { recordWithAccess = { record = { name = BName } } }
      } ) |
    # three tuple
    ( [C,B,A] = Query;
      AName = cxx1.Name A;
      BName = cxx1.Name B;
      CName = cxx1.Name C;
      { name = AName, scope =
        { namespace_ = { name = { just = BName },
          parent = { just = { name = { just = CName } } } } } |
        { recordWithAccess = { record = { name = BName,
          scope = { namespace_ = { name = { just = CName } } } |
                  { recordWithAccess = { record = { name = CName } } }
        } } }
      } ) |
    # four tuple sigh. e.g. 4 records deep , or two records in two namespaces
    ( [D,C,B,A] = Query;
      AName = cxx1.Name A;
      BName = cxx1.Name B;
      CName = cxx1.Name C;
      DName = cxx1.Name D;
      { name = AName, scope =
        { namespace_ = { name = { just = BName },
          parent = { just = { name = { just = CName },
            parent = { just = { name = { just = DName } }  }
           } } } } |
        { recordWithAccess = { record = { name = BName,
          scope = { namespace_ = { name = { just = CName },
            parent = { just = { name = { just = DName } } }
           } } |
          { recordWithAccess = { record = { name = CName,
          scope = { namespace_ = { name = { just = DName } } } |
            { recordWithAccess = { record = { name = DName } } }
           } } }
        } } }
      }

    ) = QName;

# compile to namespace qname
predicate QueryToNSQName:
  {
    query : [string],
    scope : cxx1.NamespaceQName
  }
  { Query, NSQName } where
    # zero tuple
    ( [] = Query; { nothing, nothing } ) |
    # one tuple
    ( [A] = Query; { name = { just = cxx1.Name A }} ) | # n.b. parent wild
    # two tuple
    ( [B,A] = Query;
      { just = cxx1.Name A } = AName;
      { just = cxx1.Name B } = BName;
      { name = AName, parent = { just = { name = BName } } } ) |
    # three tuple
    ( [C,B,A] = Query;
      { just = cxx1.Name A } = AName;
      { just = cxx1.Name B } = BName;
      { just = cxx1.Name C } = CName;
      { name = AName, parent = { just = { name = BName,
        parent = { just = { name = CName }}}}} ) |
    # four tuple
    ( [D,C,B,A] = Query;
      { just = cxx1.Name A } = AName;
      { just = cxx1.Name B } = BName;
      { just = cxx1.Name C } = CName;
      { just = cxx1.Name D } = DName;
      { name = AName, parent = { just = { name = BName,
        parent = { just = { name = CName,
          parent = { just = { name = DName }}}}}}} ) |
    # five tuple
    ( [E,D,C,B,A] = Query;
      { just = cxx1.Name A } = AName;
      { just = cxx1.Name B } = BName;
      { just = cxx1.Name C } = CName;
      { just = cxx1.Name D } = DName;
      { just = cxx1.Name E } = EName;
      { name = AName, parent = { just = { name = BName,
        parent = { just = { name = CName,
          parent = { just = { name = DName,
            parent = { just = { name = EName }}}}}}}}} ) |
    # six tuple
     ( [F,E,D,C,B,A] = Query;
       { just = cxx1.Name A } = AName;
       { just = cxx1.Name B } = BName;
       { just = cxx1.Name C } = CName;
       { just = cxx1.Name D } = DName;
       { just = cxx1.Name E } = EName;
       { just = cxx1.Name F } = FName;
       { name = AName, parent = { just = { name = BName,
         parent = { just = { name = CName,
           parent = { just = { name = DName,
             parent = { just = { name = EName,
               parent = { just = { name = FName } } }}}}}}}}} ) |
    # seven for real
     ( [G,F,E,D,C,B,A] = Query;
       { just = cxx1.Name A } = AName;
       { just = cxx1.Name B } = BName;
       { just = cxx1.Name C } = CName;
       { just = cxx1.Name D } = DName;
       { just = cxx1.Name E } = EName;
       { just = cxx1.Name F } = FName;
       { just = cxx1.Name G } = GName;
       { name = AName, parent = { just = { name = BName,
         parent = { just = { name = CName,
           parent = { just = { name = DName,
             parent = { just = { name = EName,
               parent = { just = { name = FName,
                 parent = { just = { name = GName }}}}}}}}}}}}}

    ) = NSQName;

}
